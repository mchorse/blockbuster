buildscript 
{
    repositories 
    {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    
    dependencies 
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'org.yaml:snakeyaml:1.18'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.4.9"
group = "mchorse.blockbuster"
archivesBaseName = "blockbuster"

/* Source compatibility */
sourceCompatibility = targetCompatibility = "1.6" // Need this here so eclipse task generates correctly.

compileJava 
{
    sourceCompatibility = targetCompatibility = "1.6"
}

/* Build language files */
task buildLangFiles {
    doLast {
        def help = file("help")
        
        def flatten
        
        /**
         * Flattens the map
         */
        flatten = { Map map, Map original, String prefix ->
            for (entry in original.entrySet())
            {
                def value = entry.getValue();
                
                if (value instanceof String)
                {
                    map.put(prefix + entry.getKey(), value)
                }
                else if (value instanceof Map)
                {
                    flatten(map, value, prefix + entry.getKey() + ".")
                }
                else
                {
                    map.put(prefix + entry.getKey(), value.toString())
                }
            }
        }
        
        /**
         * Process the string read from the YML.
         * 
         * This method replaces new line symbols to escaped new line symbols
         * and processes {} formatting to ยง Minecraft formatting.
         */
        def processString = { String str ->
            return str.replace("\n", "\\n").replaceAll('\\{([\\w\\d_]+)\\}', 'ยง$1')
        }
        
        /**
         * Compile YML file's contents to ini string
         */
        def compile = {File file ->
            def output = "\n\n# " + file.getName() + "\n";
            def yaml = new org.yaml.snakeyaml.Yaml()
            def map = yaml.load(file.text)
            def flat_map = new HashMap()
            
            flatten(flat_map, map, "")
            
            for (entry in flat_map.entrySet())
            {
                output += "${entry.getKey()}=${processString(entry.getValue())}\n";
            }
            
            return output
        }
        
        /**
         * Convert given langauge dir from YML to ini
         */
        def convert = { File lang_dir ->
            /* Forge directive that allows \n symbols to be allowed in the chat */
            def output = "#PARSE_ESCAPES"
            def output_file = file("src/main/resources/assets/${project.archivesBaseName}/lang/${lang_dir.getName()}.lang")
            
            for (file in lang_dir.listFiles())
            {
                if (file.getName().endsWith(".yml"))
                {
                    output += compile(file)
                }
            }
            
            def writer = new PrintWriter(output_file)
            
            writer.println(output.trim())
            writer.close()
        }
        
        if (help.exists())
        {
            for (lang_dir in help.listFiles())
            {
                if (lang_dir.isDirectory())
                {
                    convert(lang_dir)
                    println "Converted ${lang_dir.getName()} to language file"
                }
            }
        }
        else
        {
            println "Directory 'help' is not found!"
        }
    }
}

tasks.processResources.dependsOn('buildLangFiles')

/* Minecraft */
minecraft 
{
    version = "1.10.2-12.18.3.2185"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20161111"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    clientJvmArgs = ["-Xmx1G"]
    serverJvmArgs = ["-Xmx1G"]
}

dependencies 
{
	compile files("run/mods/metamorph-" + metamorph + "-" + project.minecraft.version + "-dev.jar")
	compile files("run/mods/aperture-" + aperture + "-" + project.minecraft.version + "-dev.jar")
}

eclipse
{
    classpath
    {
        file
        {
            whenMerged
            {
                def lib = entries.find { it.path.contains "metamorph-" + metamorph }
                lib.sourcePath = fileReference(file("run/metamorph-" + aperture + "-" + project.minecraft.version + "-source.jar"))
                
                lib = entries.find { it.path.contains "aperture-" + aperture } 
                lib.sourcePath = fileReference(file("run/aperture-" + aperture + "-" + project.minecraft.version + "-source.jar"))
            }
        }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

	// Append minecraft version to the build
    project.version += "-" + project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}